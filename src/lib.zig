const std = @import("std");

const c = @cImport({
	@cInclude("anyascii.h");
});

/// Convert a unicode codepoint to its ascii equivalent.
pub fn anyascii(allocator: std.mem.Allocator, codepoint: u21) ![]u8 {
	// Call C anyascii function.
	var cChars: [*]u8 = undefined;
	const charsCount = c.anyascii(codepoint, @ptrCast(&cChars));

	// Convert the raw C pointer to a zig allocated result.
	const result = try allocator.alloc(u8, charsCount);
	for (0..charsCount) |i| {
		result[i] = cChars[i];
	}
	return result;
}

/// Convert a unicode codepoint to its ascii equivalent, in the provided writer.
pub fn anyasciiWrite(writer: std.io.AnyWriter, codepoint: u21) !void {
	// Call C anyascii function.
	var cChars: [*]u8 = undefined;
	const charsCount = c.anyascii(codepoint, @ptrCast(&cChars));

	// Write every byte from the raw C pointer.
	for (0..charsCount) |i| {
		try writer.writeByte(cChars[i]);
	}
}

/// Convert a given UTF-8 string to its ASCII equivalent using anyascii.
pub fn utf8ToAscii(allocator: std.mem.Allocator, str: []const u8) ![]u8 {
	// Get a UTF8 iterator.
	var iterator = (try std.unicode.Utf8View.init(str)).iterator();

	// Initialize a out string array list where ascii equivalents will be appended.
	var outStr = try std.ArrayList(u8).initCapacity(allocator, str.len | 15);
	defer outStr.deinit();

	// Get a writer to the array list.
	const writer = outStr.writer().any();

	// For each codepoint, convert it to ascii.
	while (iterator.nextCodepoint()) |codepoint| {
		try anyasciiWrite(writer, codepoint);
	}

	// Return the built full ascii equivalent.
	return outStr.toOwnedSlice();
}

test anyascii {
	try testAnyascii("a", "a");
	try testAnyascii("o", "Ã¸");
	try testAnyascii("e", "Ã«");
	try testAnyascii("s", "Å");
	try testAnyascii("F", "Î¦");
	try testAnyascii(":crown:", "ğŸ‘‘");
}

/// Test the conversion of a given UTF-8 character to its ASCII equivalent.
fn testAnyascii(expectedAscii: []const u8, utf8str: []const u8) !void {
	const ascii = try anyascii(std.testing.allocator, try std.unicode.utf8Decode(utf8str));
	defer std.testing.allocator.free(ascii);
	try std.testing.expectEqualStrings(expectedAscii, ascii);
}

test utf8ToAscii {
	// These examples are taken from anyascii examples, see https://github.com/anyascii/anyascii/tree/master#examples

	try testUtf8ToAscii("a ", "Ã  ");
	try testUtf8ToAscii("a", "Ã ");
	try testUtf8ToAscii("Rene Francois Lacote", "RenÃ© FranÃ§ois LacÃ´te");
	try testUtf8ToAscii("Blosse", "BlÃ¶ÃŸe");
	try testUtf8ToAscii("Tran Hung Dao", "Tráº§n HÆ°ng Äáº¡o");
	try testUtf8ToAscii("Naeroy", "NÃ¦rÃ¸y");
	try testUtf8ToAscii("Feidippidis", "Î¦ÎµÎ¹Î´Î¹Ï€Ï€Î¯Î´Î·Ï‚");
	try testUtf8ToAscii("Dimitris Fotopoylos", "Î”Î·Î¼Î®Ï„ÏÎ·Ï‚ Î¦Ï‰Ï„ÏŒÏ€Î¿Ï…Î»Î¿Ï‚");
	try testUtf8ToAscii("Boris Nikolaevich El'tsin", "Ğ‘Ğ¾Ñ€Ğ¸Ñ ĞĞ¸ĞºĞ¾Ğ»Ğ°ĞµĞ²Ğ¸Ñ‡ Ğ•Ğ»ÑŒÑ†Ğ¸Ğ½");
	try testUtf8ToAscii("Volodimir Gorbulin", "Ğ’Ğ¾Ğ»Ğ¾Ğ´Ğ¸Ğ¼Ğ¸Ñ€ Ğ“Ğ¾Ñ€Ğ±ÑƒĞ»Ñ–Ğ½");
	try testUtf8ToAscii("T'rgovishche", "Ğ¢ÑŠÑ€Ğ³Ğ¾Ğ²Ğ¸Ñ‰Ğµ");
	try testUtf8ToAscii("ShenZhen", "æ·±åœ³");
	try testUtf8ToAscii("ShenShuiBu", "æ·±æ°´åŸ—");
	try testUtf8ToAscii("HwaSeongSi", "í™”ì„±ì‹œ");
	try testUtf8ToAscii("HuaChengShi", "è¯åŸå¸‚");
	try testUtf8ToAscii("saitama", "ã•ã„ãŸã¾");
	try testUtf8ToAscii("QiYuXian", "åŸ¼ç‰çœŒ");
	try testUtf8ToAscii("debre zeyt", "á‹°á‰¥áˆ¨ á‹˜á‹­á‰µ");
	try testUtf8ToAscii("dek'emhare", "á‹°á‰€áˆáˆ“áˆ¨");
	try testUtf8ToAscii("dmnhwr", "Ø¯Ù…Ù†Ù‡ÙˆØ±");
	try testUtf8ToAscii("Abovyan", "Ô±Õ¢Õ¸Õ¾ÕµÕ¡Õ¶");
	try testUtf8ToAscii("samt'redia", "áƒ¡áƒáƒ›áƒ¢áƒ áƒ”áƒ“áƒ˜áƒ");
	try testUtf8ToAscii("'vrhm hlvy frnkl", "××‘×¨×”× ×”×œ×•×™ ×¤×¨× ×§×œ");
	try testUtf8ToAscii("+say x ag", "â  â â â ½â €â ­â €â â ›");
	try testUtf8ToAscii("mymnsimh", "à¦®à§Ÿà¦®à¦¨à¦¸à¦¿à¦‚à¦¹");
	try testUtf8ToAscii("thntln", "á€‘á€”á€ºá€á€œá€”á€º");
	try testUtf8ToAscii("porbmdr", "àªªà«‹àª°àª¬àª‚àª¦àª°");
	try testUtf8ToAscii("mhasmumd", "à¤®à¤¹à¤¾à¤¸à¤®à¥à¤‚à¤¦");
	try testUtf8ToAscii("bemgluru", "à²¬à³†à²‚à²—à²³à³‚à²°à³");
	try testUtf8ToAscii("siemrab", "áŸáŸ€á˜ášá¶á”");
	try testUtf8ToAscii("sahvannaekhd", "àºªàº°àº«àº§àº±àº™àº™àº°à»€àº‚àº”");
	try testUtf8ToAscii("klmsseri", "à´•à´³à´®à´¶àµà´¶àµ‡à´°à´¿");
	try testUtf8ToAscii("gjpti", "à¬—à¬œà¬ªà¬¤à¬¿");
	try testUtf8ToAscii("jlmdhr", "à¨œà¨²à©°à¨§à¨°");
	try testUtf8ToAscii("rtnpur", "à¶»à¶­à·Šà¶±à¶´à·”à¶»");
	try testUtf8ToAscii("knniyakumri", "à®•à®©à¯à®©à®¿à®¯à®¾à®•à¯à®®à®°à®¿");
	try testUtf8ToAscii("srikakulm", "à°¶à±à°°à±€à°•à°¾à°•à±à°³à°‚");
	try testUtf8ToAscii("sngkhla", "à¸ªà¸‡à¸‚à¸¥à¸²");

	try testUtf8ToAscii(":crown: :palm_tree:", "ğŸ‘‘ ğŸŒ´");
	try testUtf8ToAscii("* # + 5 X", "â˜† â™¯ â™° âš„ â›Œ");
	try testUtf8ToAscii("No M & A/S", "â„– â„³ â…‹ â…");
}

/// Test the conversion of a given UTF-8 string to its ASCII equivalent.
fn testUtf8ToAscii(expectedAscii: []const u8, utf8str: []const u8) !void {
	const ascii = try utf8ToAscii(std.testing.allocator, utf8str);
	defer std.testing.allocator.free(ascii);
	try std.testing.expectEqualStrings(expectedAscii, ascii);
}
